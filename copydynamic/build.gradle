/*
 * Copyright (c) 2018 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.util.jar.JarEntry
import java.util.jar.JarFile
import java.util.zip.ZipException

plugins {
  id 'com.github.johnrengelman.shadow' version '4.0.1'
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.kotlin.kapt'
  id 'org.jetbrains.dokka'
}

//<editor-fold desc="ShadowJar configuration">
// disable default jar tasks
configurations.runtimeOnly.artifacts.removeAll { it.archiveTask.is jar }
tasks.getByName('jar').enabled = false
// create extra configuration for shaded dependencies, so they're not included in the pom
def shadedConfig = configurations.create('compileShaded')
configurations.compileOnly.extendsFrom(shadedConfig)
shadowJar {
  minimize()
  archiveClassifier.set('')
  configurations = [shadedConfig]
  relocate 'com.google', 'copydynamic.shaded.com.google'
  relocate 'afu', 'copydynamic.shaded.afu'
  relocate 'org', 'copydynamic.shaded.org'
  exclude 'javax/**'
}
artifacts {
  runtime shadowJar
  archives shadowJar
}
//</editor-fold>

dependencies {
  kapt deps.apt.autoService
  compileOnly deps.apt.autoService

  compile project(':copydynamic-annotations')

  compileShaded deps.apt.autoCommon
  compile deps.misc.kotlinpoet
  compile deps.misc.kotlinMetadata
  compile deps.kotlin.reflect
  compile deps.kotlin.stdlibjdk8
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs = ['-Xjsr305=strict', '-Xprogressive']
  }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
