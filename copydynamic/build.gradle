/*
 * Copyright (c) 2018 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.util.jar.JarEntry
import java.util.jar.JarFile
import java.util.zip.ZipException

plugins {
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.kotlin.kapt'
  id 'org.jetbrains.dokka'
}

// disable default jar tasks
configurations.runtime.artifacts.removeAll { it.archiveTask.is jar }
tasks.getByName('jar').enabled = false

// create extra configuration for shaded dependencies, so they're not included in the pom
def shadedConfig = configurations.create('compileShaded')
configurations.compileOnly.extendsFrom(shadedConfig)

shadowJar {
  classifier = ''
  configurations = [shadedConfig]

  relocate 'com.google', 'copydynamic.shaded.com.google'
  relocate 'afu', 'copydynamic.shaded.afu'
  relocate 'org', 'copydynamic.shaded.org'
  exclude 'javax/**'
}

artifacts {
  runtime shadowJar
  archives shadowJar
}

dependencies {
  kapt deps.apt.autoService
  compileOnly deps.apt.autoService

  compile project(':copydynamic-annotations')

  compileShaded deps.apt.autoCommon
  compileShaded(deps.misc.kotlinpoet) {
    transitive = false
  }
  compile deps.misc.kotlinMetadata
  compile deps.kotlin.reflect
  compile deps.kotlin.stdlibjdk8
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs = ['-Xjsr305=strict', '-Xprogressive']
  }
}

class VerifyShadowTask extends DefaultTask {

  private static final Set<String> ALLOWED_JAR_ENTRIES = new LinkedHashSet<>(Arrays.asList("io/", "copydynamic/", "META-INF/"))

  @TaskAction
  void check() {
    getLogger().debug("Verifying shadowJar contents")
    File jar = project.file("${project.buildDir}/libs")
        .listFiles()
        .find { File file ->
          file.name.endsWith(".jar") && !file.name.contains("-javadoc") && !file.name.contains("-sources")
        }

    if (jar == null) {
      throw new RuntimeException("Jar not found!")
    }

    try {
      JarFile jarFile = new JarFile(jar)
      jarFile.entries().each { JarEntry entry ->
        if (!ALLOWED_JAR_ENTRIES.any { entry.getName().startsWith(it) }) {
          throw new RuntimeException("Jar contains unexpected entry: \"${entry.getName()}\"")
        }
      }
    } catch (ZipException e) {
      throw new RuntimeException("Couldn't open ${jar.name}", e)
    }
  }
}

task verifyShadow(type: VerifyShadowTask, dependsOn: shadowJar)
tasks.check.dependsOn(verifyShadow)

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
