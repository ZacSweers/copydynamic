/*
 * Copyright (c) 2019 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id 'com.github.johnrengelman.shadow' version '4.0.1'
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.dokka'
}

//<editor-fold desc="ShadowJar configuration">
// disable default jar tasks
configurations.runtimeOnly.artifacts.removeAll { it.archiveTask.is jar }
tasks.getByName('jar').enabled = false
// create extra configuration for shaded dependencies, so they're not included in the pom
def shadedConfig = configurations.create('implementationShaded')
configurations.compileOnly.extendsFrom(shadedConfig)
shadowJar {
  minimize()
  archiveClassifier.set('')
  configurations = [shadedConfig]
  relocate 'com.google', 'metric.shaded.com.google'
  relocate 'afu', 'metric.shaded.afu'
  relocate 'org', 'metric.shaded.org'
  exclude 'javax/**'
}
artifacts {
  runtime shadowJar
  archives shadowJar
}
//</editor-fold>

dependencies {
  implementation deps.misc.kotlinpoet
  implementation deps.misc.kotlinxMetdata
  implementation deps.kotlin.reflect
  implementation deps.kotlin.stdlibjdk8
  implementationShaded deps.apt.autoCommon

  testImplementation deps.test.junit
  testImplementation deps.test.truth
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs = ['-Xjsr305=strict', '-progressive']
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs = ['-Xjsr305=strict', '-progressive']
  }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
